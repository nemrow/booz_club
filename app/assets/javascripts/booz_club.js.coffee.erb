$.backstretch("<%= image_path 'bg-img.jpg' %>")
$('[name=cell]').mask("(999) 999-9999")

placesCount = null
searchData = {}
currentRadius = 200

runFinder = (dataArray={}) ->
  searchData.placesArray = []
  placesCount = null

  overridingData = {}
  for input in dataArray
    overridingData[input.name] = input.value unless input.value.length == 0

  if @currentCoordinates
    $.extend searchData, {latitude: @currentCoordinates.lat(), longitude: @currentCoordinates.lng()}

  $.extend searchData, overridingData

  sendNearbyRequest() if validateData()

sendNearbyRequest = ->
  @latLng = new google.maps.LatLng(searchData.latitude, searchData.longitude)
  @map = new google.maps.Map($('#map-node')[0], {center: @latLng, zoom: 15})

  nearbyRequestData = {
    location: @latLng
    radius: currentRadius
    open_now: true
    types: ['liquor_store']
  }

  @placesService = new google.maps.places.PlacesService(@map)

  @placesService.nearbySearch(nearbyRequestData, nearbyCallback)

validateData = ->
  errors = {}
  if !searchData.description || searchData.description.length < 4
    errors["description"] = "You must tell us what type of booze you want. \n\n"
  if !searchData.cell || !searchData.cell.match(/\(\d{3}\) \d{3}-\d{4}/)
    errors["cell"] = "We need a correct cell phone number. "
  if !searchData.latitude || !searchData.longitude
    errors["location"] = "Make sure you allow location access in this browser."

  if Object.keys(errors).length > 0
    errorText = ""
    for key, error of errors
      errorText += error
    swal("Oops...", errorText, "error")
    resetScreen()
    false
  else
    true

nearbyCallback = (results, status) ->
  placesCount = results.length
  if currentRadius > 4828 && placesCount < 5
    swal("Sorry, not enough booz stores around you.", "Maybe you should make some moonshine?", "error")
    resetScreen()
  else if placesCount < 12
    $('.loading-container p').text("Currently found #{placesCount} possible places. Still looking...")
    setTimeout ->
      currentRadius += 200
      sendNearbyRequest()
    , 330
  else
    $('.loading-container p').text("Found #{placesCount} places. Hold on, lemme take a shot!")
    nearbyBatcher(results, 0)

nearbyBatcher = (results, currentIndex) ->
  nextIndex = currentIndex + 5
  currentBatch = results.slice(currentIndex, nextIndex)
  for place in currentBatch
    @placesService.getDetails({placeId: place.place_id}, detailCallback)
  setTimeout ->
    nearbyBatcher(results, nextIndex) if results[nextIndex] != undefined
  , 2000

detailCallback = (place, status) ->
  searchData.placesArray.push neededPlaceAttrs(place) if status == "OK"
  placesSearchComplete() if --placesCount == 0

neededPlaceAttrs = (place) ->
  name: place.name
  address: place.address_components
  types: place.types
  phone_number: place.formatted_phone_number

clearForm = () ->
  $('.status-form input[type=text]').val("")

placesSearchComplete = () ->
  placesNames = searchData.placesArray.map (place) ->
    place.name

  miles = (601 / 1600).toFixed(1)

  subText = "We found #{placesNames.length} places within #{miles} miles of you that we are checking inventory at...\n\n #{placesNames.join(', ')}"

  swal("Sweet! We will text you shortly.", subText, "success")

  clearForm()
  resetScreen()

  $.post '/search', searchData

resetScreen = () ->
  $('.status-form').slideDown()
  $('.loading-container').slideUp()

# Handle form submission
$('#find-form').submit (e) ->
  $('.loading-container p').text('Locating the nearest liquor stores')
  $('.loading-container').slideDown()
  $('.status-form').slideUp()
  runFinder($(@).serializeArray())
  e.preventDefault()

####### CHART SHIT

if $("#response-rate-chart").length > 0
  responseRateChart = $("#response-rate-chart").get(0).getContext("2d")
  stockChart = $("#stock-chart").get(0).getContext("2d")
  successChart = $("#success-chart").get(0).getContext("2d")

  chartOptions = {
    segmentShowStroke : true
    tooltipTemplate: "<%%= label %> <%%= value + '%' %>"
  }

  new Chart(responseRateChart).Doughnut(window.responseRateChartData,chartOptions)
  new Chart(stockChart).Doughnut(window.stockChartData,chartOptions)
  new Chart(successChart).Doughnut(window.successChartData,chartOptions)


####### GET THE CURRENT LOCATIONNN

initializeLocation = (pos) ->
  @currentCoordinates = new google.maps.LatLng(pos.coords.latitude, pos.coords.longitude)
  resetScreen()

locationError = (err) ->
  console.warn('ERROR(' + err.code + '): ' + err.message)

mapOptions = {
  enableHighAccuracy: true
  timeout: 75000
  maximumAge: 3000
}

navigator.geolocation.getCurrentPosition(initializeLocation, locationError, mapOptions)

windowResize = ->
  if $(window).width() < 600
    $('input[name=description]').attr("placeholder", "Name of booz you want.")
  else
    $('input[name=description]').attr("placeholder", "Name of booz you want. ex: Lagunitas Little Sumpin’ Sumpin’")

windowResize()
$(window).resize ->
  windowResize()
